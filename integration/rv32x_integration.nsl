#include "rv32x_integration.h"
#include "rv32x_core.h"
#include "opcode.h"
#include "bootrom.h"
#include "memory_map.h"
#include "clint.h"
#include "mmio_dev.h"
#include "plic.h"
#include "mmc_interface.h"

module rv32x_integration {
	rv32x_core core;
	bootrom rom;
	clint rv32x_clint;
	mmio_dev rv32x_mmio_dev;
	plic rv32x_plic;
	mmc_interface rv32x_mmcspi;

	rv32x_mmcspi.clk = mmc_clk;
	func reset {
		core.reset();
		rom.reset();
		rv32x_mmio_dev.reset();
		rv32x_mmcspi.reset();
		rv32x_plic.reset();
	}
	/* Interconnect */
	func core.imem_read {
		any {
			/* BOOTROM_BASE <= rv32x.iaddr && */ core.iaddr < BOOTROM_BASE+BOOTROM_LENGTH: rom.read((core.iaddr-BOOTROM_BASE)[11:0], MEM_WORD);
			else: imem_read(core.iaddr, core.ibyteen);
		}
	}
	func core.dmem_read {
		any {
			CLINT_BASE <= core.daddr && core.daddr < CLINT_BASE + CLINT_LENGTH:	rv32x_clint.read((core.daddr - CLINT_BASE)[15:0]);
			IO0_BASE <= core.daddr && core.daddr < IO0_BASE + IO0_LENGTH: rv32x_mmio_dev.read((core.daddr - IO0_BASE)[15:0]);
			IO1_BASE <= core.daddr && core.daddr < IO1_BASE + IO1_LENGTH: rv32x_mmcspi.read((core.daddr - IO1_BASE)[11:0], core.dbyteen);
			PLIC_BASE <= core.daddr && core.daddr < PLIC_BASE + PLIC_LENGTH: rv32x_plic.read((core.daddr));
			else: dmem_read(core.daddr, core.dbyteen);
		}
	}
	func core.dmem_write {
		any {
			CLINT_BASE <= core.daddr && core.daddr < CLINT_BASE + CLINT_LENGTH:	rv32x_clint.write((core.daddr - CLINT_BASE)[15:0], core.wdata);
			IO0_BASE <= core.daddr && core.daddr < IO0_BASE + IO0_LENGTH: rv32x_mmio_dev.write((core.daddr - IO0_BASE)[15:0], core.wdata);
			IO1_BASE <= core.daddr && core.daddr < IO1_BASE + IO1_LENGTH: rv32x_mmcspi.write((core.daddr - IO1_BASE)[11:0], core.dbyteen, core.wdata);
			PLIC_BASE <= core.daddr && core.daddr < PLIC_BASE + PLIC_LENGTH: rv32x_plic.write(core.daddr, core.wdata);
			else: dmem_write(core.daddr, core.dbyteen, core.wdata);
		}
	}
	/* End of Interconnect */

	/* PMA settings per hart */
	func core.imem_pma_check {
		any {
			/* BOOTROM_BASE <= rv32x.imem_pma_addr && */ core.imem_pma_addr < BOOTROM_BASE+BOOTROM_LENGTH: {
				core.imem_pma_executable();
				core.imem_pma_readable();
				core.imem_pma_word();
				core.imem_pma_halfword();
				core.imem_pma_cacheable();
			}
			SDRAM_BASE <= core.imem_pma_addr && core.imem_pma_addr < SDRAM_BASE + SDRAM_LENGTH: {
				core.imem_pma_executable();
				core.imem_pma_readable();
				core.imem_pma_word();
				core.imem_pma_halfword();
				core.imem_pma_cacheable();
			}
			else: core.imem_pma_empty();
		}
	}
	func core.dmem_pma_check {
		/* all location are mapped & cacheable for simulation purpose */
		any {
			CLINT_BASE <= core.dmem_pma_addr && core.dmem_pma_addr < CLINT_BASE + CLINT_LENGTH: {
				core.dmem_pma_writable();
				core.dmem_pma_readable();
				core.dmem_pma_atomic();
				core.dmem_pma_word();
			}
			IO0_BASE <= core.dmem_pma_addr && core.dmem_pma_addr < IO0_BASE + IO0_LENGTH: {
				core.dmem_pma_writable();
				core.dmem_pma_readable();
				core.dmem_pma_atomic();
				core.dmem_pma_word();
			}
			IO1_BASE <= core.dmem_pma_addr && core.dmem_pma_addr < IO1_BASE + IO1_LENGTH: {
				core.dmem_pma_writable();
				core.dmem_pma_readable();
				core.dmem_pma_atomic();
				core.dmem_pma_word();
			}
			PLIC_BASE <= core.dmem_pma_addr && core.dmem_pma_addr < PLIC_BASE + PLIC_LENGTH: {
				core.dmem_pma_writable();
				core.dmem_pma_readable();
				core.dmem_pma_atomic();
				core.dmem_pma_word();
			}
			SDRAM_BASE <= core.dmem_pma_addr && core.dmem_pma_addr < SDRAM_BASE + SDRAM_LENGTH: {
				core.dmem_pma_writable();
				core.dmem_pma_readable();
				core.dmem_pma_atomic();
				core.dmem_pma_word();
				core.dmem_pma_halfword();
				core.dmem_pma_byte();
				core.dmem_pma_cacheable();
			}
			else: core.dmem_pma_empty();
		}
	}
	/* BUS ready signals */
	func dmem_ready {
		core.rdata = rdata;
		core.dmem_ready();
	}
	func imem_ready {
		core.inst = inst;
		core.imem_ready();
	}
	func rom.ready {
		core.inst = rom.rdata;
		core.imem_ready();
	}
	func rv32x_plic.ready {
		core.rdata = rv32x_plic.rdata;
		core.dmem_ready();
	}
	func rv32x_clint.ready {
		core.rdata = rv32x_clint.rdata;
		core.dmem_ready();
	}
	func rv32x_mmcspi.valid {
		core.rdata = rv32x_mmcspi.rdata;
		core.dmem_ready();
	}
	func rv32x_mmio_dev.ready {
		core.rdata = rv32x_mmio_dev.rdata;
		core.dmem_ready();
	}
	/* BUS interrupt signals */
	func rv32x_clint.timer_interrupt_hart0 {
		core.machine_timer_interrupt_req();
	}
	func rv32x_clint.software_interrupt_hart0 {
		core.machine_software_interrupt_req();
	}
	func rv32x_plic.external_interrupt_hart_0 {
		core.machine_external_interrupt_req();
	}
	func rv32x_plic.external_interrupt_hart_1 {
		core.supervisor_external_interrupt_req();
	}
	func rv32x_mmcspi.interrupt_req{
		rv32x_plic.irq_1();
	}
	/* Device connections */
	TXD = rv32x_mmio_dev.TXD;
	rv32x_mmio_dev.RXD = RXD;
	MOSI = rv32x_mmcspi.MOSI;
	SCLK = rv32x_mmcspi.SCLK;
	CS = rv32x_mmcspi.CS;
	rv32x_mmcspi.MISO = MISO;

#ifdef HTIF
	func core.sim_done {
		sim_done(core.tohost);
	}
#endif

#ifdef CV
    core.SW = SW;
	rv32x_mmcspi.SW = SW;
	func core.debug_led_1{
		debug_led_1();
	}	
	func core.debug_led_2{
		debug_led_2();
	}	
	func core.debug_led_3{
		debug_led_3();
	}	
	func core.debug_led_4{
		debug_led_4();
	}	
	func core.debug_led_5{
		debug_led_5();
	}
	func core.debug_led_6{
		debug_led_6();
	}
	func core.debug_led_7{
		debug_led_7();
	}
	func core.debug_led_8{
		debug_led_8();
	}
	func core.debug_pc_led{
		debug_pc_led(core.pc_led);
	}
	func core.debug_sseg_h8{
		//debug_sseg_h8(core.sseg_h8);
	}
	func core.debug_sseg_l16{
		//debug_sseg_l16(core.sseg_l16);
	}
	func core.debug_sseg_l12{
		debug_sseg_l12(core.sseg_l12);
	}
	func core.debug_sseg_h12{
		debug_sseg_h12(core.sseg_h12);
	}
	func rv32x_mmcspi.debug_led_1{
		debug_led_1();
	}	
	func rv32x_mmcspi.debug_led_2{
		debug_led_2();
	}	
	func rv32x_mmcspi.debug_led_3{
		debug_led_3();
	}	
	func rv32x_mmcspi.debug_led_4{
		debug_led_4();
	}	
	func rv32x_mmcspi.debug_led_5{
		debug_led_5();
	}
	func rv32x_mmcspi.debug_led_6{
		debug_led_6();
	}
	func rv32x_mmcspi.debug_led_7{
		debug_led_7();
	}
	func rv32x_mmcspi.debug_led_8{
		debug_led_8();
	}
	func rv32x_mmcspi.debug_pc_led{
		debug_pc_led(rv32x_mmcspi.pc_led);
	}
	func rv32x_mmcspi.debug_sseg_h8{
		//debug_sseg_h8(rv32x_mmcspi.sseg_h8);
	}
	func rv32x_mmcspi.debug_sseg_l16{
		//debug_sseg_l16(rv32x_mmcspi.sseg_l16);
	}
	func rv32x_mmcspi.debug_sseg_l12{
		debug_sseg_l12(rv32x_mmcspi.sseg_l12);
	}
	func rv32x_mmcspi.debug_sseg_h12{
		debug_sseg_h12(rv32x_mmcspi.sseg_h12);
	}
#endif
}
