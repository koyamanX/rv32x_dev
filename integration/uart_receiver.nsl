#include "uart_receiver.h"
#include "fifo_8_8.h"

module uart_receiver {
	reg cnt[32] = 0;
	uart_rx_stat_t reg status = 0;
	fifo_8_8 fifo;
	proc_name idle;
	proc_name receive_start_bit;
	proc_name receive_data_bit;
	proc_name receive_stop_bit;	
	reg x0 = 1;
	reg x1 = 1;
	reg rxd = 1;
	reg clk_cnt[3] = 0;
	reg buffer[8];
	reg readbuffer[8];
	reg idx[3] = 0;
	reg bufcnt[8] = 0;
	reg threshold[8] = 64;

	x0 := RXD;
	x1 := x0;
	rxd := x1;
	
	status.full := fifo.full;
	status.empty := fifo.empty;

	
	func reset {
		idle();
	}
	proc idle {
		if(!status.busy && status.en && ~rxd) {
			status.busy := 1;
			receive_start_bit();	
		}
	}
	proc receive_start_bit {
		if(cnt == RX_BAUD_RATE_CNT) {
			cnt := 0;
			if(clk_cnt == 3) {
				if(rxd == 1) {
					clk_cnt := 0;
					status.busy := 0;
					idle();
				} else {
					clk_cnt++;
				}
			} else if(clk_cnt == 7) {
				clk_cnt := 0;
				receive_data_bit();
			} else {
				clk_cnt++;
			}
		} else {
			cnt++;
		}
	}
	proc receive_data_bit {
		if(cnt == RX_BAUD_RATE_CNT) {
			cnt := 0;
			if(clk_cnt == 3) {
				buffer := {rxd, buffer[7:1]};
				clk_cnt++;
			} else if(clk_cnt == 7) {
				clk_cnt := 0;
				if(idx == 7) {
					idx := 0;
					receive_stop_bit();
				} else {
					idx++;
				}
			} else {
				clk_cnt++;
			}
		} else {
			cnt++;
		}

	}
	proc receive_stop_bit {
		if(cnt == RX_BAUD_RATE_CNT) {
			cnt := 0;
			if(clk_cnt == 7) {
				clk_cnt := 0;
				bufcnt++;
				received_char = buffer;
				fifo.write(buffer);
				status.busy := 0;
				idle();
				done();
			} else {
				clk_cnt++;
			}
		} else {
			cnt++;
		}

	}
	func read {
		any {
			addr == 10'(0x0): {rdata = 32'(fifo.read()); bufcnt--; ready();}
			addr == 10'(0x4): {rdata = 32'(status); ready();}
			addr == 10'(0x8): {rdata = 32'(threshold); ready();}
			addr == 10'(0xc): {rdata = 32'(bufcnt); ready();}
			else: {rdata = 0x00000000; ready();}
		}
	}
	func write {
		any {
			addr == 10'(0x0): {ready();}
			addr == 10'(0x4): {status.en := wdata[0]; ready();}
			addr == 10'(0x8): {threshold := wdata[7:0]; ready();}
			addr == 10'(0xc): {ready();}
			else: {ready();}
		}
	}
}
