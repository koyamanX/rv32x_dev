#include "clint.h"
#include "csr.h"

module clint {
	reg msip = 0;
	reg mtimecmp_lo[32] = 0;
	reg mtimecmp_hi[32] = 0;
	reg mtime_lo[32] = 0;
	reg mtime_hi[32] = 0;
	wire mtimecmp[64];
	wire mtime[64];
	wire mtime_n[64];

	mtime = {mtime_hi, mtime_lo};
	mtimecmp = {mtimecmp_hi, mtimecmp_lo};

	func read {
		any {
			adrs == CLINT_MSIP_ADDR_OFFSET:		return 32'(msip);
			adrs == CLINT_MTIMECMP_ADDR_OFFSET:	return mtimecmp_lo;
			adrs == CLINT_MTIMECMP_ADDR_OFFSET
						+0x0004:				return mtimecmp_hi;
			adrs == CLINT_MTIME_ADDR_OFFSET:	return mtime_lo;
			adrs == CLINT_MTIME_ADDR_OFFSET
						+ 0x0004:				return mtime_hi;
			else: load_access_fault();
		}
		valid();
	}
	func write {
		any {
			adrs == CLINT_MSIP_ADDR_OFFSET:		msip := wdata[0];
			adrs == CLINT_MTIMECMP_ADDR_OFFSET:	{mtimecmp_lo := wdata;}
			adrs == CLINT_MTIMECMP_ADDR_OFFSET
						+0x0004:				{mtimecmp_hi := wdata;}
			adrs == CLINT_MTIME_ADDR_OFFSET: 	{mtime_lo := wdata;}
			adrs == CLINT_MTIME_ADDR_OFFSET
						+ 0x0004: 				{mtime_hi := wdata;}
			else: store_access_fault();
		}
		valid();
	}
	if(msip) {
		software_interrupt();
	}
	if(mtime >= mtimecmp) {
		timer_interrupt();
	}
	if(!write) {	/* Precisely !(write && adrs == CLINT_MTIME_ADDR_OFFSET && adrs == CLINT_MTIME_ADDR_OFFSET+0x0004) */
		mtime_n = mtime + 0x00000000_00000001;
		mtime_hi := mtime_n[63:32];
		mtime_lo := mtime_n[31:0];
	}
	func timer_interrupt_ack {
	}
	func software_interrupt_ack {
		msip := 0;
	}
}
